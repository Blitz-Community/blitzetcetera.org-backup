SeedRnd MilliSecs()

Global re$
Const Mode=1

Dim l$(3)

;Типы букв (гласные / согласные, распр. / редкие)
For n=0 To 3
 Read l$(n)
Next

;Строка с запрещенными буквосочетаниями
For n=1 To 14
 Read m$
 re$=re$+m$
Next

;Окончания фамилий
Read fend$:Read m$:fend$=fend$+m$

Graphics 800,600
SetFont LoadFont("Arial Cyr",16)

Repeat
 Cls
 For x=0 To 799 Step 160
  For y=0 To 589 Step 16
   Select Mode
    Case 1
     ;Генерация слова
     Text x,y,wordgen$(Rand(2,5),"")
    Case 2
     ;Генерация фамилии
     n=Rand(0,18)*6+1
     Text x,y,wordgen$(Rand(2,5),Mid$(fend$,n,1))+Mid$(fend$,n,6)
   End Select
  Next
 Next
Until WaitKey()=27

Data "бвгдклмнпрст"
Data "йжзфхцчшщь"
Data "аеио"
Data "ыэюяу"

Data "йа,ыа,ьа,эа,юа,яа,гб,кб,пб,фб,хб,цб,чб,шб,щб,эб,жв,йв,пв,фв,щв,"
Data "бг,гг,йг,кг,пг,фг,хг,цг,чг,шг,щг,мд,пд,фд,хд,цд,чд,шд,щд,йе,эе,юе,"
Data "вж,гж,йж,мж,пж,тж,фж,хж,цж,чж,шж,щж,ьж,эж,бз,жз,мз,пз,фз,цз,чз,шз,щз,"
Data "эз,йи,эи,бй,вй,гй,дй,жй,зй,йй,кй,лй,мй,нй,пй,рй,сй,тй,фй,хй,цй,чй,"
Data "шй,щй,ьй,щк,ък,нл,цл,щл,ъл,ьл,нм,пм,щм,ъм,фн,цн,ън,що,ыо,эо,юо,яо,"
Data "бп,гп,жп,зп,йп,кп,нп,фп,хп,цп,чп,шп,щп,ьп,юп,йр,лр,цр,шр,ьр,цс,чс,"
Data "шс,щс,жт,щт,йу,ьу,эу,юу,яу,бф,вф,гф,дф,жф,зф,йф,кф,пф,тф,хф,цф,"
Data "чф,шф,щф,ыф,юф,яф,гх,жх,зх,йх,лх,мх,пх,фх,хх,цх,чх,шх,щх,гц,зц,лц,"
Data "фц,хц,цц,чц,шц,щц,ъц,эц,юц,кч,хч,цч,чч,шч,щч,ъч,эч,жш,лш,цш,шш,щш,ъш,эш,"
Data "гщ,жщ,зщ,кщ,мщ,пщ,фщ,хщ,цщ,чщ,шщ,щщ,ъщ,эщ,аы,гы,еы,жы,иы,йы,кы,"
Data "оы,уы,хы,чы,шы,щы,ыы,ьы,эы,юы,яы,аь,гь,еь,иь,йь,кь,оь,уь,хь,ць,ыь,"
Data "ьь,эь,юь,яь,бэ,вэ,гэ,дэ,еэ,жэ,зэ,йэ,кэ,лэ,мэ,нэ,пэ,рэ,сэ,фэ,цэ,чэ,шэ,щэ,"
Data "ыэ,ьэ,ээ,юэ,яэ,вю,гю,жю,йю,кю,пю,фю,хю,цю,чю,шю,щю,ыю,эю,гя,жя,йя,"
Data "кя,фя,хя,ця,чя,шя,щя,эя,юя"

Data "ев    ов    ин    ева   ова   ина   ий    ой    ый    ая    яя    "
Data "евич  ович  енко  ец    евскаяевскийовскаяовский"

Function wordgen$(tqmax,en$)
;Тип первой буквы окончания (гласная / согласная)
ensg=(Instr("аеиоыэюяу",en$)>0)
oldsg=-1; тип предыдущей буквы
Repeat
 tq=0; счетчик переходов (гл / согл)
 lq=0; счетчик подряд идущих букв одного типа
 w$=""; результат
 oldm$=""
 Repeat
  Repeat
   ;Выбор типа буквы
   sg=Rand(0,1)
   ;Если перед этим уже стоят две буквы этого типа - смена типа
   ; (также смена типа происходит в начале фразы)
   If lq=2 Or Len(w$)=1 Then sg=1-oldsg
   ;Выбор подтипа
   n=sg*2+(Rand(0,5)=0)
   ;Проверка на наличие данного сочетания букв в списке запретных и
   ; на вариант, когда фраза начинается с "ы" или "ь"
   m$=Mid$(l$(n),Rand(1,Len(l$(n))),1)
  Until Instr(re$,oldm$+m$+",")=0 Or (oldm$="" And m$<>"ы" And m$<>"ь")
  If oldm$="" Then oldsg=sg
  If oldsg<>sg Then
   tq=tq+1
   ;Выход при достижения искомого кол-ва переходов
   If tq>tqmax Then Exit
   oldsg=sg
   lq=1
  Else
   lq=lq+1
  End If
  oldm$=m$
  w$=w$+m$
 Forever
 ;Проверка на совместимость с окончанием - наличествует ли связующее
 ; буквосочетание в списке запрещенных и есть ли переход "гласная / согласная"
Until en$="" Or (Instr(re$,oldm$+en$+",")=0 And ensg<>oldsg)
Return w$
End Function